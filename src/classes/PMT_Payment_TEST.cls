/*
    Copyright (c) 2017, Salesforce.org
    All rights reserved.

    Redistribution and use in source and binary forms, with or without
    modification, are permitted provided that the following conditions are met:

    * Redistributions of source code must retain the above copyright
      notice, this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright
      notice, this list of conditions and the following disclaimer in the
      documentation and/or other materials provided with the distribution.
    * Neither the name of Salesforce.org nor the names of
      its contributors may be used to endorse or promote products derived
      from this software without specific prior written permission.

    THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
    "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
    LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS
    FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE
    COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT,
    INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
    BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
    LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
    CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
    LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN
    ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
    POSSIBILITY OF SUCH DAMAGE.
*/
/**
* @author Salesforce.org
* @date 2017
* @group Payments
* @description tests for the PMT_Payment_TDTM class
*/
@isTest
private class PMT_Payment_TEST {
    private static final TEST_SObjectGateway.OpportunityGateway oppGateway = new TEST_SObjectGateway.OpportunityGateway();

    @TestSetup
    static void createTestData() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c= true));
        Account acc = new Account(Name = 'foo');
        insert acc;
    }

    /*******************************************************************************************************
     * @description Validate that new payment records created with an opportunity get the CurrencyIsoCode set
     * properly to the Opportunities currency code
     *******************************************************************************************************/
    static testMethod void shouldVerifyThatNewPaymentCurrencyMatchesOpportunity() {

        //only run test in multicurrency orgs
        if (!userInfo.isMultiCurrencyOrganization()) {
            return;
        }

        Account acc = [SELECT Id FROM Account LIMIT 1];

        // If there is no active non-default currency just exit out at this point.
        if (UTIL_UnitTestData_TEST.nonDefaultCurrencyType == null) {
            return;
        }

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name = 'success',
                Amount = 10,
                AccountID = acc.id,
                CloseDate = system.today(),
                StageName = UTIL_UnitTestData_TEST.getClosedWonStage());
        opp.put('CurrencyIsoCode', UTIL_UnitTestData_TEST.nonDefaultCurrencyType.IsoCode);
        insert opp;

        Test.StopTest();

        String soql = 'SELECT Id, CurrencyIsoCode FROM Opportunity WHERE Id = \'' + opp.Id + '\' LIMIT 1';
        opp = database.query(soql);

        npe01__OppPayment__c pmt = getPaymentRecord(opp.Id);
        System.AssertEquals((String) opp.get('CurrencyIsoCode'), (String) pmt.get('CurrencyIsoCode'));
    }

    /**
     * @description Validate that a change to the CurrencyIsoCode on the Opportunity record is copied
     * down the Payment records.
     */
    static testMethod void shouldVerifyThatPaymentCurrencyUpdatesFromOpportunity() {

        //only run test in multicurrency orgs
        if (!userInfo.isMultiCurrencyOrganization()) {
            return;
        }

        // If there is no active non-default currency just exit out at this point.
        if (UTIL_UnitTestData_TEST.nonDefaultCurrencyType == null) {
            return;
        }

        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name = 'success',
                Amount = 10,
                AccountID = acc.id,
                CloseDate = system.today(),
                StageName = UTIL_UnitTestData_TEST.getClosedWonStage());
        insert opp;


        // Verify that the Payment CurrencyIsoCode is updated when the Opportunity CurrencyIsoCode is changed
        opp.put('CurrencyIsoCode', UTIL_UnitTestData_TEST.nonDefaultCurrencyType.IsoCode);
        update opp;

        Test.stopTest();

        npe01__OppPayment__c pmt = getPaymentRecord(opp.Id);
        System.AssertEquals((String) opp.get('CurrencyIsoCode'), (String) pmt.get('CurrencyIsoCode'));
        System.AssertEquals(opp.Amount, (Double)pmt.npe01__Payment_Amount__c);
    }

    /**
     * @description Validate that an error is thrown when the CurrencyIsoCode is modified on Payment record directly
     * or when a new payment is added with a different currency code than the parent opportunity.
     */
    static testMethod void shouldVerifyThatPaymentCurrencyChangesAreBlocked() {

        //only run test in multicurrency orgs
        if (!userInfo.isMultiCurrencyOrganization()) {
            return;
        }

        // If there is no active non-default currency just exit out at this point.
        if (UTIL_UnitTestData_TEST.nonDefaultCurrencyType == null) {
            return;
        }

        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='success',
                Amount = 10,
                AccountID=acc.id,
                CloseDate=system.today(),
                StageName=UTIL_UnitTestData_TEST.getClosedWonStage());
        insert opp;

        npe01__OppPayment__c pmt = getPaymentRecord(opp.Id);

        // Verify that individual changes to the Payment CurrencyIsoCode are blocked
        pmt.put('CurrencyIsoCode', UTIL_UnitTestData_TEST.nonDefaultCurrencyType.IsoCode);
        try {
            update pmt;
            System.Assert(False, 'Updating the Payment.CurrencyIsoCode directly should have failed');
        } catch (Exception ex) {
            // GOOD!
            System.Assert(ex.getMessage().contains(System.Label.pmtModifyCurrency), 'Error message should contain "' +
                    System.Label.pmtModifyCurrency + '"');
        }

        // Verify that a new Payment cannot be inserted with a different currencyIsoCode
        pmt = new npe01__OppPayment__c(
            npe01__Opportunity__c = opp.Id,
            npe01__Payment_Amount__c = 100
        );
        pmt.put('CurrencyIsoCode', UTIL_UnitTestData_TEST.nonDefaultCurrencyType.IsoCode);
        try {
            insert pmt;
            System.Assert(False, 'Inserting the Payment.CurrencyIsoCode directly should have failed');
        } catch (Exception ex) {
            // GOOD!
            System.Assert(ex.getMessage().contains(System.Label.pmtModifyCurrency), 'Error message should contain "' +
                    System.Label.pmtModifyCurrency + '"');
        }
    }

    /**
     * @description Test that when Advanced Currency Management (ACM) is enabled, the logic that determines
     * when an Opportunity should be auto-closed still executes properly. In this scenario, the roll-ups from
     * Payment to the Opportunity object do not work, so a trigger on the Payment object is used instead.
     * This test validates that logic by setting mock to pretend that ACM is enabled.
     */
    private static testMethod void shouldVerifyThatOpportunityWillAutoCloseWithACMEnabled() {

        // Pretend the advanced multi-currency is enabled
        // Note: Trigger methods need to use UserInfo.isMultiCurrencyOrganization() to determine if the
        // CurrencyIsoCode should be queried to avoid an error when multi-currency is actually enabled
        // in an organization.
        UTIL_Currency_TEST.UtilCurrencyMock mock = new UTIL_Currency_TEST.UtilCurrencyMock();
        mock.getCurrencySymbolReturn = 'USD';
        mock.isMultiCurrencyOrganizationReturn = true;
        mock.isAdvancedCurrencyManagementEnabledReturn = true;
        UTIL_Currency.instance = mock;

        String closedWonStage = UTIL_UnitTestData_TEST.getClosedWonStage();
        String openStage = UTIL_UnitTestData_TEST.getOpenStage();

        if (String.isEmpty(closedWonStage) || String.isEmpty(openStage)) {
            System.debug(
                    System.LoggingLevel.ERROR,
                    'Organization settings do not contain the necessary opportunity stages to run this test. Not running test.'
            );
            return;
        }

        // Disable auto payment creation fro this test
        UTIL_CustomSettingsFacade.getContactsSettings().npe01__Payments_Enabled__c = false;
        UTIL_CustomSettingsFacade.getContactsSettings().Payments_Auto_Close_Stage_Name__c = closedWonStage;

        Account acc = [SELECT Id FROM Account LIMIT 1];

        Opportunity opp = new Opportunity(
                Name='success',
                Amount = 500,
                AccountID=acc.id,
                CloseDate=system.today(),
                StageName=openStage);
        insert opp;

        List<npe01__OppPayment__c> pmts = new List<npe01__OppPayment__c>{
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 250,
                        npe01__Opportunity__c = opp.Id
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 250,
                        npe01__Opportunity__c = opp.Id
                )
        };

        Test.startTest();

        insert pmts;

        opp = [SELECT Name, Amount, StageName, IsClosed, IsWon FROM Opportunity WHERE Id = :opp.Id LIMIT 1];
        System.assertEquals(openStage, opp.StageName, 'The Opportunity should still be in an open state');

        // Mark the payment as Paid
        pmts[1].npe01__Paid__c = true;
        update pmts;

        Test.stopTest();

        // Verify that the Opportunity is now closed
        /*opp = [SELECT Name, Amount, StageName, IsClosed, IsWon, npe01__Number_Of_Payments__c,
                npe01__Amount_Outstanding__c, npe01__Payments_Made__c,
                (SELECT npe01__Paid__c, npe01__Payment_Amount__c, npe01__Written_Off__c FROM npe01__OppPayment__r)
                FROM Opportunity
                WHERE Id = :opp.Id LIMIT 1];*/
        opp = [SELECT Name, Amount, StageName, IsClosed, IsWon FROM Opportunity WHERE Id = :opp.Id LIMIT 1];
        System.assertEquals(closedWonStage, opp.StageName);
    }


    /*******************************************************************************************************
     * @description     Validate that payment records are not both paid and written off on Update
     *******************************************************************************************************/
    @isTest
    private static void shouldVerifyPaymentsAreNotBothPaidAndWrittenOffOnUpdate() {
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Opportunity opp = new Opportunity(
            Name = 'success',
            Amount = 400,
            AccountID = acc.id,
            CloseDate = System.today(),
            StageName = UTIL_UnitTestData_TEST.getOpenStage()
        );

        insert opp;

        npe01__OppPayment__c payment = getPaymentRecord(opp.Id);

        Test.startTest();
        try {
            // Attempt to update an existing system
            payment.npe01__Paid__c = true;
            payment.npe01__Written_Off__c = true;

            update payment;
            System.assert(false, 'Updating an existing payment with both written off and paid checked should fail');
        } catch (Exception ex) {
            System.assert(ex.getMessage().contains(System.Label.pmtWriteOffPaidPayment), 'Error message should contain "' +
                System.Label.pmtWriteOffPaidPayment + '"');
        }

        try {
            payment.npe01__Paid__c = false;
            payment.npe01__Written_Off__c = false;
            update payment;

            payment.npe01__Paid__c = true;
            payment.npe01__Written_Off__c = false;
            update payment;

            payment.npe01__Paid__c = false;
            payment.npe01__Written_Off__c = true;
            update payment;

        } catch (Exception ex) {
            System.assert(false, 'Updating an existing payment with valid values should succeed');
        }
        Test.stopTest();
    }

    /*******************************************************************************************************
     * @description     Validate that payment records are not both paid and written off on Insert
     *******************************************************************************************************/
    @isTest
    private static void shouldVerifyPaymentsAreNotBothPaidAndWrittenOffOnInsert() {
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Opportunity opp = new Opportunity(
            Name = 'success',
            Amount = 400,
            AccountID = acc.id,
            CloseDate = System.today(),
            StageName = UTIL_UnitTestData_TEST.getOpenStage()
        );

        insert opp;

        // Attempt to create a new payment with both fields set to true
        npe01__OppPayment__c newPayment = new npe01__OppPayment__c();
        newPayment.npe01__Opportunity__c = opp.Id;
        newPayment.npe01__Payment_Amount__c = 400;
        newPayment.npe01__Payment_Date__c = System.today();
        newPayment.npe01__Scheduled_Date__c = System.today();
        newPayment.npe01__Paid__c = true;
        newPayment.npe01__Written_Off__c = true;

        Test.startTest();
        try {
            insert newPayment;
            System.assert(False, 'Inserting an existing payment with both written off and paid checked should fail');
        } catch (Exception ex) {
            System.assert(ex.getMessage().contains(System.Label.pmtWriteOffPaidPayment), 'Error message should contain "' +
                System.Label.pmtWriteOffPaidPayment + '"');
        }

        // You should still be able to insert new payments without paid or written off
        List<npe01__OppPayment__c> newPayments = new List<npe01__OppPayment__c>();
        newPayment.npe01__Written_Off__c = false;
        newPayment.npe01__Paid__c = false;
        newPayment.npe01__Payment_Amount__c = 100;
        newPayments.add(newPayment);

        npe01__OppPayment__c p2 = newPayment.clone();
        p2.npe01__Paid__c = false;
        p2.npe01__Written_Off__c = true;
        p2.npe01__Payment_Amount__c = 200;
        newPayments.add(p2);

        npe01__OppPayment__c p3 = newPayment.clone();
        p3.npe01__Paid__c = true;
        p3.npe01__Written_Off__c = false;
        p3.npe01__Payment_Amount__c = 300;
        newPayments.add(p3);

        insert newPayments;

        List<npe01__OppPayment__c> insertedPayments = [
            SELECT Id, Name, npe01__Paid__c, npe01__Written_Off__c, npe01__Payment_Amount__c
            FROM npe01__OppPayment__c];

        System.assertEquals(insertedPayments.size(), 4, 'Valid payment records should be created');

        Test.stopTest();
    }

    /**
     * @description Validate that the accounting rules are enforced properly.
     * If there is a paid or written off payment, it must have a payment date.
     */
     @isTest
    static void shouldPreventMissingPaymentDateWhenEnforceAccting() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c= true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Opportunity opp = new Opportunity(
                Name = 'PreventMissingPaymentDateTest',
                Amount = 10,
                AccountID = acc.id,
                CloseDate = System.today(),
                StageName = UTIL_UnitTestData_TEST.getClosedWonStage());
        insert opp;

        Test.startTest();

        // Test the invalid paid insertion case
        npe01__OppPayment__c invalidPaidPayment = new npe01__oppPayment__c(npe01__Opportunity__c = opp.Id, npe01__Paid__c = true);
        try {
            insert invalidPaidPayment;
            System.assert(false, 'Inserting a paid payment without a paid date will fail');
        } catch (Exception e) {
            System.assert(e.getMessage().contains(System.Label.pmtPaidPaymentDateRequired), 'The error message is not expected, actual: ' + e.getMessage());
        }

        // Test the valid paid payment insertion case
        npe01__OppPayment__c validPaidPayment = new npe01__oppPayment__c(npe01__Opportunity__c = opp.Id, npe01__Paid__c = true, npe01__Payment_Date__c = Date.today());
        insert validPaidPayment;
        assertDataForPayment('Accounting enforced, Insert Paid Payment with payment date', validPaidPayment.Id, true, false, System.today(), null);

        // Test the invalid paid payment update case
        invalidPaidPayment = new npe01__oppPayment__c(npe01__Opportunity__c = opp.Id, npe01__Paid__c = true, npe01__Payment_Date__c = Date.Today());
        insert invalidPaidPayment;

        invalidPaidPayment = updatePaymentForTesting(invalidPaidPayment, true, false, false, false);
        try {
            update invalidPaidPayment;
            System.assert(false, 'Updating a payment to paid without a paid date will fail');
        } catch (Exception e) {
            System.assert(e.getMessage().contains(System.Label.pmtPaidPaymentDateRequired), 'The error message is not expected, actual: ' + e.getMessage());
        }

        // Test the valid paid payment update case
        validPaidPayment = updatePaymentForTesting(validPaidPayment, true, false, true, false);
        update validPaidPayment;
        assertDataForPayment('Accounting enforced, Update Paid Payment with payment date', validPaidPayment.Id, true, false, System.today(), null);

        // Test the invalid written off insertion case
        npe01__OppPayment__c invalidWrittenOffPayment = new npe01__oppPayment__c(npe01__Opportunity__c = opp.Id, npe01__Written_Off__c = true);
        try {
            insert invalidWrittenOffPayment;
            System.assert(false, 'Inserting a written off payment without a paid date will fail');
        } catch (Exception e) {
            System.assert(e.getMessage().contains(System.Label.pmtWrittenOffPaymentDateRequired), 'The error message is not expected, actual: ' + e.getMessage());
        }

         // Test the valid written off payment insertion case
        npe01__OppPayment__c validWrittenOffPayment = new npe01__oppPayment__c(npe01__Opportunity__c = opp.Id, npe01__Written_Off__c = true, npe01__Payment_Date__c = Date.today());
        insert validWrittenOffPayment;
        assertDataForPayment('Accounting enforced, Insert Written Off Payment with payment date', validWrittenOffPayment.Id, false, true, System.today(), null);

        invalidWrittenOffPayment = new npe01__OppPayment__c(npe01__Opportunity__c = opp.Id, npe01__Written_Off__c = true, npe01__Payment_Date__c = Date.today());
        insert invalidWrittenOffPayment;

        // Test the invalid written off payment update case
        invalidWrittenOffPayment = updatePaymentForTesting(invalidWrittenOffPayment, false, true, false, false);

        try {
            update invalidWrittenOffPayment;
            System.assert(false, 'Updating a payment to written off without a paid date will fail');
        } catch (Exception e) {
            System.assert(e.getMessage().contains(System.Label.pmtWrittenOffPaymentDateRequired), 'The error message is not expected, actual: ' + e.getMessage());
        }

        // Test the valid written off payment update case
        validWrittenOffPayment = updatePaymentForTesting(validWrittenOffPayment, false, true, true, false);
        update validWrittenOffPayment;
        assertDataForPayment('Accounting enforced, Update Written Off Payment with payment date', validWrittenOffPayment.Id, false, true, System.today(), null);

        Test.stopTest();
    }

    /**
     * @description Validate that the accounting rules are enforced properly.
     * If it is an unpaid payment, it must have a scheduled date.
     */
     @isTest
    static void shouldPreventMissingScheduledDateWhenEnforceAccting() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c= true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Opportunity opp = new Opportunity(
                Name = 'PreventMissingScheduledDateTest',
                Amount = 10,
                AccountID = acc.id,
                CloseDate = system.today(),
                StageName = UTIL_UnitTestData_TEST.getClosedWonStage());
        insert opp;

        Test.startTest();

        // Test the invalid unpaid insertion case
        npe01__OppPayment__c invalidUnpaidPayment = new npe01__oppPayment__c(npe01__Opportunity__c = opp.Id, npe01__Paid__c = false);
        try {
            insert invalidUnpaidPayment;
            System.assert(false, 'Inserting a scheduled payment without a scheduled date will fail');
        } catch (Exception e) {
            System.assert(e.getMessage().contains(System.Label.pmtUnpaidPaymentScheduledDateRequired), 'The error message is not expected, actual: ' + e.getMessage());
        }

       // Test the valid unpaid payment insertion case
        npe01__OppPayment__c validUnpaidPayment = new npe01__oppPayment__c(npe01__Opportunity__c = opp.Id, npe01__Paid__c = false, npe01__Scheduled_Date__c = Date.today());
        insert validUnpaidPayment;
        assertDataForPayment('Accounting enforced, Insert Unpaid Payment with scheduled date', validUnpaidPayment.Id, false, false, null, System.today());

        // Test the invalid unpaid payment update case
        validUnpaidPayment = updatePaymentForTesting(validUnpaidPayment, false, false, false, false);
        try {
            update validUnpaidPayment;
            System.assert(false, 'Updating a scheduled payment without a scheduled date will fail');
        } catch (Exception e) {
            System.assert(e.getMessage().contains(System.Label.pmtUnpaidPaymentScheduledDateRequired), 'The error message is not expected, actual: ' + e.getMessage());
        }

        // Test the valid unpaid payment update case
        validUnpaidPayment = updatePaymentForTesting(validUnpaidPayment, false, false, false, true);
        update validUnpaidPayment;
        assertDataForPayment('Accounting enforced, Update Unpaid Payment with scheduled date', validUnpaidPayment.Id, false, false, null, System.today());

        Test.stopTest();
    }

    /**
     * @description Validate that the accounting rules are not enforced
     * when Enforce Accounting Data Consistency is not checked.
     * If there is a paid or written off payment, nothing additional is enforced.
     */
     @isTest
    static void shouldAllowMissingPaymentDateWhenNotEnforceAccting() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c= true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Opportunity opp = new Opportunity(
                Name = 'AllowMissingPaymentDateTest',
                Amount = 10,
                AccountID = acc.id,
                CloseDate = system.today(),
                StageName = UTIL_UnitTestData_TEST.getClosedWonStage());
        insert opp;

        Test.startTest();

        // Test the invalid paid insertion case
        npe01__OppPayment__c invalidPaidPayment = new npe01__oppPayment__c(npe01__Opportunity__c = opp.Id, npe01__Paid__c = true);
        insert invalidPaidPayment;
        assertDataForPayment('Accounting not enforced, Insert Paid Payment without payment date', invalidPaidPayment.Id, true, false, null, null);

        // Test the invalid written off insertion case
        npe01__OppPayment__c invalidWrittenOffPayment = new npe01__oppPayment__c(npe01__Opportunity__c = opp.Id, npe01__Written_Off__c = true);
        insert invalidWrittenOffPayment;
        assertDataForPayment('Accounting not enforced, Insert Written Off Payment without payment date', invalidWrittenOffPayment.Id, false, true, null, null);

       // Test the invalid paid payment update case
        invalidPaidPayment = updatePaymentForTesting(invalidPaidPayment, true, false, false, false);
        update invalidPaidPayment;
        assertDataForPayment('Accounting not enforced, Update Paid Payment without payment date', invalidPaidPayment.Id, true, false, null, null);

       // Test the invalid written off payment update case
        invalidWrittenOffPayment = updatePaymentForTesting(invalidWrittenOffPayment, false, true, false, false);
        update invalidWrittenOffPayment;
        assertDataForPayment('Accounting not enforced, Update Written Off Payment without payment date', invalidWrittenOffPayment.Id, false, true, null, null);

        Test.stopTest();
    }

    /**
     * @description Validate that the accounting rules are not enforced
     * when Enforce Accounting Data Consistency is not checked.
     * If there is a scheduled payment, nothing additional is enforced
     */
     @isTest
    static void shouldAllowMissingScheduledDateWhenNotEnforceAccting() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c= true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Opportunity opp = new Opportunity(
                Name = 'AllowMissingScheduledDateTest',
                Amount = 10,
                AccountID = acc.id,
                CloseDate = system.today(),
                StageName = UTIL_UnitTestData_TEST.getClosedWonStage());
        insert opp;

        Test.startTest();

        // Test the invalid unpaid insertion case
        npe01__OppPayment__c invalidUnpaidPayment = new npe01__oppPayment__c(npe01__Opportunity__c = opp.Id, npe01__Paid__c = false);
        insert invalidUnpaidPayment;
        invalidUnpaidPayment = getPaymentRecordByPaymentId(invalidUnpaidPayment.Id);
        assertDataForPayment('Accounting not enforced, Insert Unpaid without schedule date', invalidUnpaidPayment.Id, false, false, null, null);

        // Test the invalid unpaid update case
        invalidUnpaidPayment = updatePaymentForTesting(invalidUnpaidPayment, false, false, false, false);
        update invalidUnpaidPayment;
        assertDataForPayment('Accounting not enforced, Update Unpaid without schedule date', invalidUnpaidPayment.Id, false, false, null, null);
        Test.stopTest();
    }


    /*******************************************************************************************************
     * HELPER FUNCTIONS
     *******************************************************************************************************/

    /*******************************************************************************************************
    * @description Retrieves payment and asserts key date for Payment concerning accounting and payment dates
    * @param String testName Used as part of the assertion text that will display if the assertion fails
    * @param Id paymentId Payment Id to check
    * @param Boolean isPaid value to assert against npe01__Paid__c
    * @param Boolean isWrittenOff value to assert against npe01__Written_Off__c
    * @param Date paidDate value to assert against npe01__Payment_Date__c
    * @param Date scheduledDate value to assert against npe01__Scheduled_Date__c
    * @return void Does not return anything
    ********************************************************************************************************/
    private static void assertDataForPayment(String testName, Id paymentId, Boolean isPaid, Boolean isWrittenOff, Date paidDate, Date scheduledDate) {
        npe01__OppPayment__c pmt = getPaymentRecordByPaymentId(paymentId);
        System.assertEquals (isPaid, pmt.npe01__Paid__c, testName + ': Paid');
        System.assertEquals (isWrittenOff, pmt.npe01__Written_Off__c, testName + ': Written Off');
        System.assertEquals (paidDate, pmt.npe01__Payment_Date__c, testName + ': Payment Date');
        System.assertEquals (scheduledDate, pmt.npe01__Scheduled_Date__c, testName + ': Scheduled Date');
    }

    /**
     * @description Helper function to help set the payment attributes as necessary for testing that accounting data consistency is enforced.
     */
    private static npe01__OppPayment__c updatePaymentForTesting(npe01__OppPayment__c payment, Boolean isPaid, Boolean isWrittenOff, Boolean hasPaymentDate, Boolean hasScheduledDate) {
        payment.npe01__Paid__c = isPaid;
        payment.npe01__Written_Off__c = isWrittenOff;

        if (hasPaymentDate == true) {
            payment.npe01__Payment_Date__c = Date.today();
        } else {
            payment.npe01__Payment_Date__c = null;
        }

        if (hasScheduledDate == true) {
            payment.npe01__Scheduled_Date__c = Date.today();
        } else {
            payment.npe01__Scheduled_Date__c = null;
        }

        return payment;
    }

    /**
     * @description Reusable utility method to retrieve a specific payment record using the payments Id.
     */
    private static npe01__OppPayment__c getPaymentRecordByPaymentId(Id paymentId) {

        String soql = 'SELECT Id, npe01__Payment_Amount__c, npe01__Paid__c, npe01__Written_Off__c, npe01__Payment_Date__c, npe01__Scheduled_Date__c ';
        if (UserInfo.isMultiCurrencyOrganization()) {
            soql += ', CurrencyIsoCode ';
        }
        soql += ' FROM npe01__OppPayment__c WHERE Id = \'' + paymentId + '\' LIMIT 1';
        return (npe01__OppPayment__c)Database.query(soql);
    }

    /**
     * @description Test that when Advanced Currency Management (ACM) is enabled, the logic that determines
     * when an Opportunity should be auto-closed still executes properly for overpaid opportunities. In this scenario, the roll-ups from
     * Payment to the Opportunity object do not work, so a trigger on the Payment object is used instead.
     * This test validates that logic by setting mock to pretend that ACM is enabled.
     */
    @isTest
    private static void testOverpaidOpportunityWillAutoCloseWithACMEnabled() {

        // Pretend the advanced multi-currency is enabled
        // Note: Trigger methods need to use UserInfo.isMultiCurrencyOrganization() to determine if the
        // CurrencyIsoCode should be queried to avoid an error when multi-currency is actually enabled
        // in an organization.
        UTIL_Currency_TEST.UtilCurrencyMock mock = new UTIL_Currency_TEST.UtilCurrencyMock();
        mock.getCurrencySymbolReturn = 'USD';
        mock.isMultiCurrencyOrganizationReturn = true;
        mock.isAdvancedCurrencyManagementEnabledReturn = true;
        UTIL_Currency.instance = mock;

        String closedWonStage = UTIL_UnitTestData_TEST.getClosedWonStage();
        String openStage = UTIL_UnitTestData_TEST.getOpenStage();

        if (String.isEmpty(closedWonStage) || String.isEmpty(openStage)) {
            System.debug(
                    System.LoggingLevel.ERROR,
                    'Organization settings do not contain the necessary opportunity stages to run this test. Not running test.'
            );
            return;
        }

        // Disable auto payment creation fro this test
        UTIL_CustomSettingsFacade.getContactsSettings().npe01__Payments_Enabled__c = false;
        UTIL_CustomSettingsFacade.getContactsSettings().Payments_Auto_Close_Stage_Name__c = closedWonStage;

        Account acc = [SELECT Id FROM Account LIMIT 1];

        Opportunity opp = new Opportunity(
                Name='success',
                Amount = 500,
                AccountID=acc.id,
                CloseDate=system.today(),
                StageName=openStage);
        insert opp;

        List<npe01__OppPayment__c> pmts = new List<npe01__OppPayment__c>{
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 250,
                        npe01__Opportunity__c = opp.Id
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 250,
                        npe01__Opportunity__c = opp.Id
                )
        };

        Test.startTest();

        insert pmts;

        opp = [SELECT Name, Amount, StageName, IsClosed, IsWon FROM Opportunity WHERE Id = :opp.Id LIMIT 1];
        System.assertEquals(openStage, opp.StageName, 'The Opportunity should still be in an open state');

        // Mark the payment as Paid
        pmts[1].npe01__Payment_Amount__c = 500;
        pmts[1].npe01__Paid__c = true;
        update pmts;

        Test.stopTest();

        // Verify that the Opportunity is now closed
        /*opp = [SELECT Name, Amount, StageName, IsClosed, IsWon, npe01__Number_Of_Payments__c,
                npe01__Amount_Outstanding__c, npe01__Payments_Made__c,
                (SELECT npe01__Paid__c, npe01__Payment_Amount__c, npe01__Written_Off__c FROM npe01__OppPayment__r)
                FROM Opportunity
                WHERE Id = :opp.Id LIMIT 1];*/
        opp = [SELECT Name, Amount, StageName, IsClosed, IsWon FROM Opportunity WHERE Id = :opp.Id LIMIT 1];
        System.assertEquals(closedWonStage, opp.StageName);
    }

    /**
     * @description Test that the logic that determines when an Opportunity should be auto-closed still executes 
     * properly for overpaid opportunities. In this scenario, the roll-ups from
     * Payment to the Opportunity object do not work, so a trigger on the Payment object is used instead.
     */
    @isTest
    private static void testOverpaidOpportunityWillAutoClose() {

        String closedWonStage = UTIL_UnitTestData_TEST.getClosedWonStage();
        String openStage = UTIL_UnitTestData_TEST.getOpenStage();

        if (String.isEmpty(closedWonStage) || String.isEmpty(openStage)) {
            System.debug(
                    System.LoggingLevel.ERROR,
                    'Organization settings do not contain the necessary opportunity stages to run this test. Not running test.'
            );
            return;
        }

        // Disable auto payment creation for this test
        UTIL_CustomSettingsFacade.getContactsSettings().Payments_Auto_Close_Stage_Name__c = closedWonStage;

        Account acc = [SELECT Id FROM Account LIMIT 1];

        Opportunity opp = new Opportunity(
                Name='success',
                Amount = 500,
                AccountID=acc.id,
                CloseDate=system.today(),
                StageName=openStage);
        insert opp;

        List<npe01__OppPayment__c> pmts = [Select Id from npe01__oppPayment__c Where npe01__Opportunity__c =: opp.Id];

        Test.startTest();

        opp = [SELECT Name, Amount, StageName, IsClosed, IsWon FROM Opportunity WHERE Id = :opp.Id LIMIT 1];
        System.assertEquals(openStage, opp.StageName, 'The Opportunity should still be in an open state');

        // Mark the payment as Paid
        pmts[0].npe01__Payment_Amount__c = 1000;
        pmts[0].npe01__Paid__c = true;
        update pmts;

        Test.stopTest();

        // Verify that the Opportunity is now closed
        /*opp = [SELECT Name, Amount, StageName, IsClosed, IsWon, npe01__Number_Of_Payments__c,
                npe01__Amount_Outstanding__c, npe01__Payments_Made__c,
                (SELECT npe01__Paid__c, npe01__Payment_Amount__c, npe01__Written_Off__c FROM npe01__OppPayment__r)
                FROM Opportunity
                WHERE Id = :opp.Id LIMIT 1];*/
        opp = [SELECT Name, Amount, StageName, IsClosed, IsWon FROM Opportunity WHERE Id = :opp.Id LIMIT 1];
        System.assertEquals(closedWonStage, opp.StageName);
    }

    /**
     * @description Test that the logic that determines when an Opportunity should be auto-closed still executes 
     * properly. In this scenario, the roll-ups from
     * Payment to the Opportunity object do not work, so a trigger on the Payment object is used instead.
     */
    @isTest
    private static void testOpportunityWillAutoClose() {

        String closedWonStage = UTIL_UnitTestData_TEST.getClosedWonStage();
        String openStage = UTIL_UnitTestData_TEST.getOpenStage();

        if (String.isEmpty(closedWonStage) || String.isEmpty(openStage)) {
            System.debug(
                    System.LoggingLevel.ERROR,
                    'Organization settings do not contain the necessary opportunity stages to run this test. Not running test.'
            );
            return;
        }

        // Disable auto payment creation for this test
        UTIL_CustomSettingsFacade.getContactsSettings().Payments_Auto_Close_Stage_Name__c = closedWonStage;

        Account acc = [SELECT Id FROM Account LIMIT 1];

        Opportunity opp = new Opportunity(
                Name='success',
                Amount = 500,
                AccountID=acc.id,
                CloseDate=system.today(),
                StageName=openStage);
        insert opp;

        List<npe01__OppPayment__c> pmts = [Select Id from npe01__oppPayment__c Where npe01__Opportunity__c =: opp.Id];

        Test.startTest();

        opp = [SELECT Name, Amount, StageName, IsClosed, IsWon FROM Opportunity WHERE Id = :opp.Id LIMIT 1];
        System.assertEquals(openStage, opp.StageName, 'The Opportunity should still be in an open state');

        // Mark the payment as Paid
        pmts[0].npe01__Payment_Amount__c = 500;
        pmts[0].npe01__Paid__c = true;
        update pmts;

        Test.stopTest();

        // Verify that the Opportunity is now closed
        /*opp = [SELECT Name, Amount, StageName, IsClosed, IsWon, npe01__Number_Of_Payments__c,
                npe01__Amount_Outstanding__c, npe01__Payments_Made__c,
                (SELECT npe01__Paid__c, npe01__Payment_Amount__c, npe01__Written_Off__c FROM npe01__OppPayment__r)
                FROM Opportunity
                WHERE Id = :opp.Id LIMIT 1];*/
        opp = [SELECT Name, Amount, StageName, IsClosed, IsWon FROM Opportunity WHERE Id = :opp.Id LIMIT 1];
        System.assertEquals(closedWonStage, opp.StageName);
    }

    /**
    * @description Verifies that when auto payment is not enabled and payments are fully received
    *    the Opp will be updated to the Closed per Payment Auto Close State settings
    */
    @isTest
    private static void oppStageShouldClosedWhenPaymentsAreFullyReceivedAndAutoPaymentNotEnabled() {
        UTIL_CustomSettingsFacade.getContactsSettings().npe01__Payments_Enabled__c = false;
        UTIL_CustomSettingsFacade.getContactsSettings().Payments_Auto_Close_Stage_Name__c = UTIL_UnitTestData_TEST.getClosedWonStage();

        Account acc = [SELECT Id FROM Account LIMIT 1];
        Opportunity opp = new TEST_OpportunityBuilder()
            .withAccount(acc.Id)
            .withOpenStage()
            .withCloseDate(System.today())
            .withAMount(500)
            .build();

        insert opp;

        List<npe01__OppPayment__c> pmts = new List<npe01__OppPayment__c>{
            new npe01__OppPayment__c(
                    npe01__Paid__c = true,
                    npe01__Payment_Amount__c = 250,
                    npe01__Opportunity__c = opp.Id
            ),
            new npe01__OppPayment__c(
                    npe01__Paid__c = true,
                    npe01__Payment_Amount__c = 250,
                    npe01__Opportunity__c = opp.Id
            )
        };

        Test.startTest();
        insert pmts;
        Test.stopTest();

        opp = oppGateway.getRecord(opp.Id);

        System.assertEquals(UTIL_UnitTestData_TEST.getClosedWonStage(), opp.StageName,
            'Opportunity should be automatically closed when payments are fully received and payment enabled is off');
    }

    /**
    * @description Verifies that when auto payment is enabled and payments are fully received,
    *    the Opp will be updated to the Closed per Payment Auto Close State settings
    */
    @isTest
    private static void oppStageShouldClosedWhenPaymentsAreFullyReceivedAndAutoPaymentEnabled() {
        UTIL_CustomSettingsFacade.getContactsSettings().npe01__Payments_Enabled__c = true;
        UTIL_CustomSettingsFacade.getContactsSettings().Payments_Auto_Close_Stage_Name__c = UTIL_UnitTestData_TEST.getClosedWonStage();

        Account acc = [SELECT Id FROM Account LIMIT 1];

        Opportunity opp = new TEST_OpportunityBuilder()
            .withAccount(acc.Id)
            .withOpenStage()
            .withCloseDate(System.today())
            .withAMount(500)
            .build();

        insert opp;

        List<npe01__OppPayment__c> pmts = new List<npe01__OppPayment__c>{
            new npe01__OppPayment__c(
                    npe01__Paid__c = true,
                    npe01__Payment_Amount__c = 250,
                    npe01__Opportunity__c = opp.Id
            ),
            new npe01__OppPayment__c(
                    npe01__Paid__c = true,
                    npe01__Payment_Amount__c = 250,
                    npe01__Opportunity__c = opp.Id
            )
        };

        Test.startTest();
        insert pmts;
        Test.stopTest();

        opp = oppGateway.getRecord(opp.Id);

        System.assertEquals(UTIL_UnitTestData_TEST.getClosedWonStage(), opp.StageName,
            'Opportunity should be automatically closed when payments are fully received and payment enabled is off');
    }

    /* Test with Enforce Accounting Data Consistency Enabled */
    /*
     * @description Check that the opportunity amount is updated when the payed payments amounts
     * exceed the opportunity amount if the opp is not finalized
     */
    @isTest
    private static void paidPaymentAddLargerAmtToOppNonFinalized() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='committed opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_COMMITTED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 250,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 250,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                )
        };
        insert payments;

        Test.stopTest();

        // check the opp amount
        opp = [select Id, Amount from Opportunity where Id = :opp.Id];
        System.assertEquals(500, opp.Amount, 'Open opportunity amount not updated when paid amounts exceed opportunity amount.');
    }

    /* Test with Enforce Accounting Data Consistency Enabled */
    /*
     * @description Check that the finalized opportunity amount is updated when the payed payments amounts
     * are less than the opportunity amount
     */
    @isTest
    private static void paidPaymentAddLowerAmtToFinalized() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='finalized opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_FINALIZED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 10,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 10,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                )
        };
        insert payments;

        Test.stopTest();

        // check the opp amount
        opp = [select Id, Amount from Opportunity where Id = :opp.Id];
        System.assertEquals(20, opp.Amount, 'Finalized opportunity amount not updated when paid amounts are less than opportunity amount.');
    }

    /* Test with Enforce Accounting Data Consistency Enabled */
    /* @description Non Finalized Opportunity, Amount changed to more than Sum of Paid Payments: Result (Allowed)
    */
    @isTest
    private static void oppNonFinalizedUpdateAmtToHigherThanPaidPayments() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='committed opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_COMMITTED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 50,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 50,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                )
        };
        insert payments;
        
        Test.stopTest();

        // update the opp to a larger amount
        try {
            opp.Amount = 300;
            update opp;

            // success
            opp = [select Id, Amount from Opportunity where Id = :opp.Id];
            System.assertEquals(300, opp.Amount, 'The non finalized opportunity amount should have updated to a larger amount.');

        } catch (Exception ex) {
            // failed
            System.Assert(false, 'The non finalized opportunity amount should have updated to a larger amount.');
        }

    }

    /* Test with Enforce Accounting Data Consistency Enabled */
    /* @description Non Finalized Opportunity, Amount changed to less than Sum of Paid Payments: Result (Blocked)
    */
    @isTest
    private static void oppNonFinalizedUpdateAmtToLowerThanPaidPayments() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='committed opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_COMMITTED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 50,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 50,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                )
        };
        insert payments;
        
        Test.stopTest();

        // update the opp to a lower amount, should be blocked
        try {
            opp.Amount = 37;
            update opp;

            // failure
            System.assert(false, 'The opportunity amount should not have updated to a lower amount.');

        } catch (Exception ex) {
            // success
            System.assert(ex.getMessage().contains(System.Label.Opp_Amount_Is_Less_Error), 'The opportunity was not updated, but received different error message.');
        }

    }

    /* Test with Enforce Accounting Data Consistency Enabled */
    /* @description Finalized Opportunity, Amount same as Sum of Paid Payments: Result (Allowed)
    */
    @isTest
    private static void oppFinalizedUpdateAmtToEqualToPaidPayments() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='finalized opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_FINALIZED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 40,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 40,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                )
        };
        insert payments;

        Test.stopTest();

        // update opp amt to equal payments
         try {
            opp.Amount = 80;
            update opp;

            // allowed
            System.assert(true);
         } catch (Exception ex) {
            // failed
            System.assert(false, 'Finalized opportunity did not update to amount equal to paid payments.');
        }
    }

    /* Test with Enforce Accounting Data Consistency Enabled */
    /* @description Finalized Opportunity, Amount < Sum of Paid Payments: Result (Blocked)
    */
    @isTest
    private static void oppFinalizedUpdateAmtToLowerThanPaidPayments() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='finalized opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_FINALIZED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 50,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 50,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                )
        };
        insert payments;
        
        Test.stopTest();

        // update the opp to a lower amount, should be blocked
        try {
            opp.Amount = 37;
            update opp;

            // failure
            System.assert(false, 'The opportunity amount should not have updated to a lower amount.');

        } catch (Exception ex) {
            // success
            System.assert(ex.getMessage().contains(System.Label.Opp_Amount_Not_Equal_Error), 'The opportunity was not updated, but received different error message.');
        }
    }
    
    /* Test with Enforce Accounting Data Consistency Enabled */
    /* @description Finalized Opportunity, Amount > Sum of Paid Payments: Result (Blocked)
    */
    @isTest
    private static void oppFinalizedUpdateAmtToHigherThanPaidPayments() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='finalized opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_FINALIZED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 50,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 50,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                )
        };
        insert payments;
        
        Test.stopTest();

        // update the opp to a higher amount, should be blocked
        try {
            opp.Amount = 175;
            update opp;

            // failure
            System.assert(false, 'The opportunity amount should not have updated to a higher amount.');

        } catch (Exception ex) {
            // success
            System.assert(ex.getMessage().contains(System.Label.Opp_Amount_Not_Equal_Error), 'The opportunity was not updated, but received different error message.');
        }
    }

    /* Test with Enforce Accounting Data Consistency Enabled */
    /* @description Non Final Opportunity inserted with unpaid payments totaling < than opportunity Amount. Change Unpaid Payments to Paid. Amount should remain the same
    */
    @isTest
    private static void oppNonFinalizedUpdatePaymentsToPaid() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='committed opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_COMMITTED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 40,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Scheduled_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 40,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Scheduled_Date__c = System.today()
                )
        };
        insert payments;

        // update payments to paid
        for (npe01__OppPayment__c p : payments) {
            p.npe01__Paid__c = true;
            p.npe01__Payment_Date__c = System.today();
        }
        update payments;
        
        Test.stopTest();

        // check the opp amount
        opp = [select Id, Amount from Opportunity where Id = :opp.Id];
        System.assertEquals(100, opp.Amount, 'The opportunity amount should have remained unchanged.');
        
    }


    /* Test with Enforce Accounting Data Consistency Enabled */
    /* @description Non Finalized Opportunity inserted with unpaid payments totaling > than opportunity Amount. 
                    Change Unpaid Payments to Paid. Amount should increase to Sum of Paid Payments
    */
    @isTest
    private static void oppNonFinalizedUpdateHigherPaymentsToPaid() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='committed opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_COMMITTED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 60,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Scheduled_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 60,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Scheduled_Date__c = System.today()
                )
        };
        insert payments;

        // update payments to paid
        for (npe01__OppPayment__c p : payments) {
            p.npe01__Paid__c = true;
            p.npe01__Payment_Date__c = System.today();
        }
        update payments;
        
        Test.stopTest();

        // check the opp amount
        opp = [select Id, Amount from Opportunity where Id = :opp.Id];
        System.assertEquals(120, opp.Amount, 'The opportunity amount should have increased.');
    }

    /* Test with Enforce Accounting Data Consistency Enabled */
    /* @description Non Finalized Opportunity inserted with unpaid payments totaling < Opportunity Amount. 
       Change Unpaid Payments to Paid. No change to Opportunity Amount. Change Opportunity to Finalized Stage. 
       Amount should change to Sum of Paid Payments
    */
    @isTest
    private static void oppNonFinalizedUpdateLowerPaymentsToPaidMakeFinalized() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='committed opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_COMMITTED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 40,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Scheduled_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 40,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Scheduled_Date__c = System.today()
                )
        };
        insert payments;

        // update payments to paid
        for (npe01__OppPayment__c p : payments) {
            p.npe01__Paid__c = true;
            p.npe01__Payment_Date__c = System.today();
        }
        update payments;
        
        Test.stopTest();

        // check the opp amount
        opp = [select Id, Amount from Opportunity where Id = :opp.Id];
        System.assertEquals(100, opp.Amount, 'The opportunity amount should have remained unchanged.');

        // make the opp finalized
        opp.StageName = OPP_StageMappingUtil.TEST_FINALIZED_STAGE;
        update opp;

        // check the opp amount again
        opp = [select Id, Amount from Opportunity where Id = :opp.Id];
        System.assertEquals(80, opp.Amount, 'The opportunity amount should have decreased.');
    }
    
    /* Test with Enforce Accounting Data Consistency Enabled */
    /* @description Non Finalized Opportunity inserted with unpaid payments. Change to Finalized Opportunity. 
       Update the payments to Paid. Confirm Opportunity Amount is now Sum of Paid Payments: 
       Note: this test will eventually conflict with write off on finalized behavior.
    */
    @isTest
    private static void oppNonFinalizedToFinalizedUpatePaymentsToPaid() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='committed opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_COMMITTED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 40,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Scheduled_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 40,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Scheduled_Date__c = System.today()
                )
        };
        insert payments;

        // make the opp finalized
        opp.StageName = OPP_StageMappingUtil.TEST_FINALIZED_STAGE;
        update opp;

        // update payments to paid
        for (npe01__OppPayment__c p : payments) {
            p.npe01__Paid__c = true;
            p.npe01__Payment_Date__c = System.today();
        }
        update payments;
        
        Test.stopTest();

        // check the opp amount
        opp = [select Id, Amount from Opportunity where Id = :opp.Id];
        System.assertEquals(80, opp.Amount, 'The opportunity amount should have decreased.');
    }

    /* Test with Enforce Accounting Data Consistency Enabled */
    /* @description Finalized Opportunity with Paid Payment equal to amount. Add additional unpaid payment. No change in Opp amount. 
        Add additional paid payment confirm Opp AMount increases.
    */
    @isTest
    private static void oppFinalizedWithEqualPaidPaymentsAddUnpaidPayment() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='finalized opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_FINALIZED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 50,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 50,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                )
        };
        insert payments;

        // add aditional unpaid payment
        npe01__OppPayment__c unpaidP = new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 25,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Scheduled_Date__c = System.today()
                );
        insert unpaidP;
        
        Test.stopTest();

        // check the opp amount
        opp = [select Id, Amount from Opportunity where Id = :opp.Id];
        System.assertEquals(100, opp.Amount, 'The opportunity amount should have remained unchanged.');

        // add paid payment
        npe01__OppPayment__c paidP = new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 35,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                );
        insert paidP;

        // check the opp amount again
        opp = [select Id, Amount from Opportunity where Id = :opp.Id];
        System.assertEquals(135, opp.Amount, 'The opportunity amount should have increased.');
    }

    /* Test with Enforce Accounting Data Consistency Enabled */
    /* @description Finalized Opportunity with Paid Payments equal to Amount. Change one of the Payments to Unpaid or Written Off. Confirm Opportunity Amount updates.
    */
    @isTest
    private static void oppFinalizedWithEqualPaidPaymentsWriteOffPayment() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = true));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='finalized opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_FINALIZED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 50,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 50,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                )
        };
        insert payments;

        Test.stopTest();

        // update one the payments to unpaid
        npe01__OppPayment__c unpaidP = payments[0];
        unpaidP.npe01__Paid__c = false;
        unpaidP.npe01__Payment_Date__c = null;
        unpaidP.npe01__Scheduled_Date__c = System.today();
        update unpaidP;

        // check the opp amount again
        opp = [select Id, Amount from Opportunity where Id = :opp.Id];
        System.assertEquals(50, opp.Amount, 'The opportunity amount should have decreased.');
    }

    /* Test with Enforce Accounting Data Consistency Not Enabled */
    /* @description Create Non Finalized Opportunity with Amount > Paid Payments. Change Opp Amount to < Sum of Paid Payment Amounts. Confirm no error.
    */
    @isTest
    private static void oppNonFinalizedUpdateAmountToLowerThanPaymentsDataConsistDisabled() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = false));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='committed opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_COMMITTED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 40,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 40,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                )
        };
        insert payments;

        Test.stopTest();

        // update the opp to a lower amount, should not get an error
        opp.Amount = 27;
        update opp;

        // check the opp amount again
        opp = [select Id, Amount from Opportunity where Id = :opp.Id];
        System.assertEquals(27, opp.Amount, 'The opportunity amount should have decreased.');

    }

    /* Test with Enforce Accounting Data Consistency Not Enabled */
    /* @description Create Non Finalized Opportunity with Amount < Unpaid Payments. Make Payments Paid. Confirm No change to Opp Amount.
    */
    @isTest
    private static void oppNonFinalizedWAmountLowerThanPaidPaymentsMarkPaymentsPaidDataConsistDisabled() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = false));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='committed opp',
                Amount = 80,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_COMMITTED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 50,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Scheduled_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 50,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Scheduled_Date__c = System.today()
                )
        };
        insert payments;

        // make the payments paid
        for (npe01__OppPayment__c p : payments) {
            p.npe01__Paid__c = true;
            p.npe01__Payment_Date__c = System.today();
        }
        update payments;

        Test.stopTest();

        // check the opp amount again, it should be unchanged
        opp = [select Id, Amount from Opportunity where Id = :opp.Id];
        System.assertEquals(80, opp.Amount, 'The opportunity amount should have stayed the same.');
    }

    /* Test with Enforce Accounting Data Consistency Not Enabled */
    /* @description Create Finalized Opportunity With Amount Different than Paid Payments. Confirm no error. Update Amount. Confirm no error.
    */
    @isTest
    private static void oppFinalizedWAmountDiffThanPaidPaymentsUpdateAmountDataConsistDisabled() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = false));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='finalized opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_FINALIZED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 40,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = true,
                        npe01__Payment_Amount__c = 40,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Payment_Date__c = System.today()
                )
        };
        insert payments;

        // confirm no error
        System.assert(true);

        // update opp amount; should be allowed
        opp.Amount = 35;
        update opp;

        // check the opp amount again, it should be changed
        opp = [select Id, Amount from Opportunity where Id = :opp.Id];
        System.assertEquals(35, opp.Amount, 'The opportunity amount should have updated.');
    }

    /* Test with Enforce Accounting Data Consistency Not Enabled */
    /* @description Create Finalized Opportunity With Amount and Unpaid Payments. Change Unpaid Payments to Paid. Confirm no change in amount.
    */
    @isTest
    private static void oppFinalizedWUnpaidPaymentsUpdateeToPaidDataConsistDisabled() {
        UTIL_CustomSettingsFacade.getContactsSettingsForTests(new npe01__Contacts_And_Orgs_Settings__c (npe01__Payments_Enabled__c = true, Enforce_Accounting_Data_Consistency__c = false));
        Account acc = [SELECT Id FROM Account LIMIT 1];

        Test.startTest();

        Opportunity opp = new Opportunity(
                Name='finalized opp',
                Amount = 100,
                AccountID = acc.Id,
                CloseDate = System.today(),
                npe01__Do_Not_Automatically_Create_Payment__c = true,
                StageName = OPP_StageMappingUtil.TEST_FINALIZED_STAGE);
        insert opp;

        List<npe01__OppPayment__c> payments = new List<npe01__OppPayment__c> {
                new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 40,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Scheduled_Date__c = System.today()
                ),
                new npe01__OppPayment__c(
                        npe01__Paid__c = false,
                        npe01__Payment_Amount__c = 40,
                        npe01__Opportunity__c = opp.Id,
                        npe01__Scheduled_Date__c = System.today()
                )
        };
        insert payments;

        // make payments paid
        for (npe01__OppPayment__c p : payments) {
            p.npe01__Paid__c = true;
            p.npe01__Payment_Date__c = System.today();
        }
        update payments;

        // check the opp amount again, it should be unchanged
        opp = [select Id, Amount from Opportunity where Id = :opp.Id];
        System.assertEquals(100, opp.Amount, 'The opportunity amount should have remained unchanged.');
    }


    /**
     * @description Reusable utility method to retrieve a specific payment record for an Opportunity
     */
    private static npe01__OppPayment__c getPaymentRecord(Id oppId) {

        String soql = 'SELECT Id, npe01__Payment_Amount__c ';
        if (UserInfo.isMultiCurrencyOrganization()) {
            soql += ', CurrencyIsoCode ';
        }
        soql += ' FROM npe01__OppPayment__c WHERE npe01__Opportunity__c = \'' + oppId + '\' LIMIT 1';
        return (npe01__OppPayment__c)Database.query(soql);
    }
}